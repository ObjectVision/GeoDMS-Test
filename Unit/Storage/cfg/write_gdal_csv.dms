container Write_gdal_csv: url = "https://github.com/ObjectVision/GeoDMS/issues/391"
{
	unit<uint32> reference: nrofrows = 5
	{
		attribute<int32>   IntegerAtt : [0,1,256,-100,9999];
		attribute<float32> FloatAtt   : [0,1,999999,-2.5,99.9];
		attribute<string>  StringAtt  : ['Hello','Test',null,'Two words','88a'];
	}

	unit<uint32> att: nrofrows = 3
	{
		attribute<string> name : ['IntegerAtt', 'FloatAtt', 'StringAtt'];
	}

	container export :=
		for_each_ind('nedvat'
			, att/name
			, 'string(reference/' + att/name + ')'
			, reference
			, string
			, '%localDataDir%/GeoDMSTestResults/unit/storage/write_gdal_csv/' + att/name +  '.csv'
			, 'gdalwrite.vect');
		
	unit<uint32> export_all := select_with_attr_by_cond(reference, isDefined(id(reference)))
		, StorageName = "%localDataDir%/GeoDMSTestResults/unit/storage/write_gdal_csv/all.csv"
		, StorageType =	"gdalwrite.vect";


	container reRead_IntegerAtt: StorageName = "%localDataDir%/GeoDMSTestResults/unit/storage/write_gdal_csv/IntegerAtt.csv", StorageType = "gdal.vect"
	{
		attribute<int32> IntegerAtt (reference);
	}

	container reRead_FloatAtt: StorageName = "%localDataDir%/GeoDMSTestResults/unit/storage/write_gdal_csv/FloatAtt.csv", StorageType = "gdal.vect"
	{
		attribute<float32> FloatAtt (reference);
	}

	container reRead_StringAtt: StorageName = "%localDataDir%/GeoDMSTestResults/unit/storage/write_gdal_csv/StringAtt.csv", StorageType = "gdal.vect"
	{
		attribute<string> StringAtt (reference);
	}

	container reRead_all: StorageName = "%localDataDir%/GeoDMSTestResults/unit/storage/write_gdal_csv/all.csv", StorageType = "gdal.vect"
	{
		attribute<int32>   IntegerAtt (reference);
		attribute<float32> FloatAtt   (reference);
		attribute<string>  StringAtt  (reference);
	}

	attribute<bool> test (reference) := 
		   reRead_IntegerAtt/IntegerAtt == reference/IntegerAtt
		&& reRead_FloatAtt/FloatAtt     == reference/FloatAtt
		&& reRead_StringAtt/StringAtt   == reference/StringAtt

		&& reRead_all/IntegerAtt        == reference/IntegerAtt
		&& reRead_all/FloatAtt          == reference/FloatAtt
		&& reRead_all/StringAtt         == reference/StringAtt
		;

	parameter<string> test_log := all(test) 
		? '' 
		: 'Write Storage gdal csv Test: ' + expand(., '%configDir%') + '.dms ' + PropValue(test, 'fullname') + ' result: not OK'
	,	StorageType = "str"
	,	storagename = "%localDataDir%/GeoDMSTestResults/unit/storage/Write_gdal_csv.txt";
}